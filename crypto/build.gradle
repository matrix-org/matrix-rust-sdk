buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.19.0'
    }
}

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

apply plugin: "com.vanniktech.maven.publish"

android {
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

android.libraryVariants.all { variant ->
    tasks.register("generate${variant.name.capitalize()}UniffiBindings", Exec) {
        // Runs the bindings generation, note that you must have uniffi-bindgen installed
        // and in your PATH environment variable
        commandLine 'uniffi-bindgen', 'generate', '../rust-sdk-crypto/src/olm.udl',
                '--language', 'kotlin',
                '--out-dir', "${buildDir}/generated/source/uniffi/${variant.name}/java"
    }

    def sourceSet = variant.sourceSets.find { it.name == variant.name }
    sourceSet.java.srcDir new File(buildDir, "generated/source/uniffi/${variant.name}/java")
}

dependencies {
    implementation 'net.java.dev.jna:jna:5.10.0@aar'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}